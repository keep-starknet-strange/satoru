//! Vault for withdrawals.

// *************************************************************************
//                                  IMPORTS
// *************************************************************************

// Core lib imports.
use starknet::ContractAddress;

// *************************************************************************
//                  Interface of the `WithdrawalVault` contract.
// *************************************************************************
#[starknet::interface]
trait IWithdrawalVault<TContractState> {
    /// Initialize the contract.
    /// # Arguments
    /// * `data_store_address` - The address of the data store contract.
    /// * `role_store_address` - The address of the role store contract.
    fn initialize(
        ref self: TContractState,
        data_store_address: ContractAddress,
        role_store_address: ContractAddress,
    );

    /// Transfer tokens from this contract to a receiver.
    /// # Arguments
    /// * `token` - The token address to transfer.
    /// * `receiver` - The address of the receiver.
    /// * `amount` - The amount of tokens to transfer.
    fn transfer_out(
        ref self: TContractState, token: ContractAddress, receiver: ContractAddress, amount: u256,
    );

    /// Records a token transfer into the contract.
    /// # Arguments
    /// * `token` - The token address to transfer.
    /// # Returns
    /// * The amount of tokens transferred.
    fn record_transfer_in(ref self: TContractState, token: ContractAddress) -> u256;

    /// This can be used to update the tokenBalances in case of token burns
    /// or similar balance changes
    /// the prevBalance is not validated to be more than the nextBalance as this
    /// could allow someone to block this call by transferring into the contract    
    /// # Arguments
    /// * `token` - The token to record the burn for
    /// # Return
    /// * The new balance
    fn sync_token_balance(ref self: TContractState, token: ContractAddress) -> u256;
}

#[starknet::contract]
mod WithdrawalVault {
    // *************************************************************************
    //                               IMPORTS
    // *************************************************************************

    // Core lib imports.
    use core::zeroable::Zeroable;
    use starknet::{ContractAddress};

    // Local imports.
    use satoru::role::role_store::{IRoleStoreDispatcher, IRoleStoreDispatcherTrait};
    use satoru::data::data_store::{IDataStoreDispatcher, IDataStoreDispatcherTrait};
    use satoru::bank::strict_bank::{StrictBank, IStrictBank};
    use satoru::withdrawal::error::WithdrawalError;

    // *************************************************************************
    //                              STORAGE
    // *************************************************************************
    #[storage]
    struct Storage {
        /// Interface to interact with the `DataStore` contract.
        data_store: IDataStoreDispatcher,
        /// Interface to interact with the `RoleStore` contract.
        role_store: IRoleStoreDispatcher,
    }

    // *************************************************************************
    //                              CONSTRUCTOR
    // *************************************************************************

    /// Constructor of the contract.
    /// # Arguments
    /// * `role_store_address` - The address of the role store contract.
    /// * `data_store_address` - The address of the data store contract.
    #[constructor]
    fn constructor(
        ref self: ContractState,
        data_store_address: ContractAddress,
        role_store_address: ContractAddress,
    ) {
        self.data_store.write(IDataStoreDispatcher { contract_address: data_store_address });
        self.role_store.write(IRoleStoreDispatcher { contract_address: role_store_address });
    }

    // *************************************************************************
    //                          EXTERNAL FUNCTIONS
    // *************************************************************************
    #[abi(embed_v0)]
    impl BankImpl of super::IWithdrawalVault<ContractState> {
        fn initialize(
            ref self: ContractState,
            data_store_address: ContractAddress,
            role_store_address: ContractAddress,
        ) {
            let mut state: StrictBank::ContractState = StrictBank::unsafe_new_contract_state();
            IStrictBank::initialize(ref state, data_store_address, role_store_address);
        }

        fn transfer_out(
            ref self: ContractState,
            token: ContractAddress,
            receiver: ContractAddress,
            amount: u256,
        ) {
            let mut state: StrictBank::ContractState = StrictBank::unsafe_new_contract_state();
            IStrictBank::transfer_out(ref state, token, receiver, amount);
        }

        fn record_transfer_in(ref self: ContractState, token: ContractAddress) -> u256 {
            let mut state: StrictBank::ContractState = StrictBank::unsafe_new_contract_state();
            IStrictBank::record_transfer_in(ref state, token)
        }

        fn sync_token_balance(ref self: ContractState, token: ContractAddress) -> u256 {
            let mut state: StrictBank::ContractState = StrictBank::unsafe_new_contract_state();
            IStrictBank::sync_token_balance(ref state, token)
        }
    }
}
